plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'checkstyle'
    id 'jacoco'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.owasp.dependencycheck' version '6.2.2'
    id 'com.github.ben-manes.versions' version '0.39.0'
    id 'com.jfrog.bintray' version '1.8.5'
}

//group 'uk.gov.hmcts.reform'
group 'com.github.hmcts'
version '0.1.168-ecm-137'

sourceCompatibility = '11.0'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

// region test coverage

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = false
    }
}

// endregion

checkstyle {
    maxWarnings = 172
    toolVersion = '8.29'
}

// region publishing

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'ecm-common'
            version project.version
        }
    }
}

ext {
    reformLoggingVersion = '5.1.9'
    log4JVersion = '2.17.0'
}

// endregion


dependencies {
    api group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '4.0.2'
    api group: 'org.apache.poi', name: 'poi-ooxml', version: '5.0.0'
    api group: 'org.elasticsearch', name: 'elasticsearch', version: '7.13.2'
    api group: 'com.microsoft.azure', name: 'azure-servicebus', version: '3.6.4'
    // CVE-2020-11987, CVE-2020-11988 (batik-1.13)
    // Can be removed when we upgrade poi-ooxml to 6.0
    // https://bz.apache.org/bugzilla/show_bug.cgi?id=65166
    api group: 'org.apache.xmlgraphics', name: 'batik-all', version: '1.14'

    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.7'
    implementation group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
    implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: reformLoggingVersion
    implementation group: 'com.github.hmcts.java-logging', name: 'logging-appinsights', version: reformLoggingVersion
    implementation group: 'com.github.sps.junidecode', name: 'junidecode', version: '0.3'
    implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'
    implementation group: 'com.google.guava', name: 'guava', version: '30.1.1-jre'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4JVersion
    implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: log4JVersion

    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'

    testImplementation group: 'io.rest-assured', name: 'rest-assured', version: '4.4.0'
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.11.2'
    testImplementation group: 'org.springframework', name: 'spring-test', version: '5.3.8'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.20.2'
    testImplementation 'pl.pragmatists:JUnitParams:1.1.1'
    testCompileOnly 'org.projectlombok:lombok:1.18.20'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'
}

jar {
    enabled = true
}